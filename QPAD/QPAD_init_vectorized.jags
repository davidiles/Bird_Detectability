
    model {
    
    dummy ~ dunif(0,1)
    
    # ------------------------------
    # Simulation
    # ------------------------------
    
    # For each simulated bird, probability it has generated at least one detectable cue by end of time interval
    for (i in 1:Nsim){
    
      # Simulate cueing for each bird in each time interval
      for (j in 1:ntint){
      
        # Expected number of cues produced up to end of interval
        lambda[i,j] <- phi*tint[j]*exp(-(sim_dist[i]/tau)^2)
        
        # Probability bird produces at least one detectable cue by end of interval
        p[i,j] <- 1 - exp(-lambda[i,j])
      }
    }
    
   # Sum up number of detected birds in each time and distance bin (this is sort of equivalent to the CDF)
    for (i in 1:nrint){
      for (j in 1:ntint){
        Dsim_matrix[i,j] <- sum(p[r_from[i]:r_to[i],j])
      }
    }
   
   # Difference to calculate cell probabilities
    for (i in 1:nrint){
      Ysim_matrix[i,1] <- Dsim_matrix[i,1]
      for (j in 2:ntint){
        Ysim_matrix[i,j] <- Dsim_matrix[i,j] - sum(Dsim_matrix[i,(j-1)])
      }
    } 
    
    
    # Place counts in correct vectorized bins
    for (i in 1:n_bins_vectorized){
        Ysim_vectorized[i] <- Ysim_matrix[Y_rint_numeric[i],Y_tint_numeric[i]]
      }
  }
    

